group 'me.ele'
version '0.3'
apply plugin: 'java'
apply plugin: 'bintray-release'
apply plugin: 'maven'
dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile gradleApi()
    compileOnly "com.android.tools.build:gradle:$android_tools_version"
    compile "com.google.code.gson:gson:2.8.0"
    compile project(':lancet-weaver')
//    compile 'me.ele:lancet-weaver:0.3'
    testCompile gradleTestKit()
    testCompile 'junit:junit:4.12'
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

dependencies {
    testRuntime files(createClasspathManifest)
}
publish {
    userOrg = 'tellh'//bintray.com用户名
    groupId = "$upload_group"//jcenter上的路径
    artifactId = 'lancet-plugin'//项目名称
    publishVersion = "$upload_version"//版本号
    desc = 'Add proxy check to lancet '
    website = 'https://github.com/eleme/lancet'
}
uploadArchives {
    repositories {
        mavenDeployer {
            //设置插件的GAV参数
            pom.groupId = 'me.ele'
            pom.artifactId = 'lancet-plugin'
            pom.version = 0.3
            //文件发布到下面目录
            repository(url: uri('../gradle_plugins'))
        }
    }
}